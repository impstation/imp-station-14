uniform highp vec4 colour;

uniform sampler2D noise_0;
uniform sampler2D noise_1;
uniform sampler2D noise_2;

const highp float tileSize = 32.0;

void fragment() {

	highp vec2 rounded = quantiseVecDown(UV, tileSize); //downsample to a 32 * 32 grid for stylistic consistency

	COLOR = 0.25 * texture(noise_0, loopVec(vec2(rounded.x + 0.2 + TIME * 0.3, rounded.y)));
	COLOR += 0.25 * texture(noise_0, loopVec(vec2(rounded.x - 0.5 - TIME * 0.3, rounded.y)));
	COLOR += 0.25 * texture(noise_0, loopVec(vec2(rounded.x, rounded.y + 0.1 - TIME * 0.3)));
	COLOR += 0.25 * texture(noise_0, loopVec(vec2(rounded.x, rounded.y + 0.7 + TIME * 0.3)));

    highp vec3 asHSV = rgb2hsv(COLOR.rgb);
    asHSV.y -= sin(TIME * 0.5) * 0.25;
    asHSV.z *= 1.0 + (COLOR.a * COLOR.a * COLOR.a);//todo need to figure out a way to make brighter spots much brighter while keeping darker spots dark?
	COLOR = vec4(hsv2rgb(asHSV), COLOR.a);
	COLOR *= colour;
}

highp vec2 quantiseVecDown(highp vec2 v, highp float divisions) {
    highp float multiple = 1.0 / divisions;
    highp vec2 rem = mod(v, multiple);
    return v - rem;
}

highp float quantiseFloatDown(highp float f, highp float divisions) {
    highp float multiple = 1.0 / divisions;
    highp float rem = mod(f, multiple);
    return f - rem;
}

highp vec3 rgb2hsv(highp vec3 c) {
    highp vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
    highp vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
    highp vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));

    highp float d = q.x - min(q.w, q.y);
    highp float e = 0.00000000010; //because this doesn't support doing 1.0e-10
    return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
}

highp vec3 hsv2rgb(highp vec3 c) {
    highp vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    highp vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

//hate that I have to do this because we can't just set the loop behaviour on the texture
highp vec2 loopVec(highp vec2 toLoop) {
    return abs(mod(toLoop, 1));
}
